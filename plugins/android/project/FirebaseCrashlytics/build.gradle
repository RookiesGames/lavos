plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

apply from: '../config.gradle'

String pluginName = "FirebaseCrashlytics"
String pluginPackageName = "eu.rookies.google.firebase.crashlytics"

android {
    namespace pluginPackageName
    compileSdk versions.compileSdk
    buildToolsVersion versions.buildTools
    defaultConfig {
        minSdk versions.minSdk
        targetSdk versions.targetSdk

        consumerProguardFiles "consumer-rules.pro"

        manifestPlaceholders["godotPluginName"] = pluginName
        manifestPlaceholders["godotPluginPackageName"] = pluginPackageName
        buildConfigField("String", "GODOT_PLUGIN_NAME", "\"${pluginName}\"")
        setProperty("archivesBaseName", pluginName)
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility versions.javaVersion
        targetCompatibility versions.javaVersion
    }
    kotlinOptions {
        jvmTarget = versions.javaVersion
    }
    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${paths.lavosRoot}/addons/google/firebase/crashlytics/.bin/android/FirebaseCrashlytics.${variant.buildType.name}.aar"
        }
    }
}

dependencies {
    compileOnly files(godotLib.templateAAR)
    implementation firebaseLibraries.crashlyticsKtxLib
    implementation firebaseLibraries.crashlyticsNdkLib
    implementation firebaseLibraries.analyticsKtxLib

    implementation libraries.kotlinStdLib
    implementation libraries.appCompatLib
}
